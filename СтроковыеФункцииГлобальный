// Вырезает подстроки из строки на основании указанных границ в параметрах ТекстДо и ТексПосле.
//
// Параметры:
//   Строка					- Строка - исходная строка.
//   ТекстДо 				- Строка - левая граница искомой подстроки.
//   ТекстПосле 			- Строка - правая граница искомой подстроки. 
//   ПолучитьВсеВхождения 	- Булево - возвращаются все вхождения подстрок в массиве.
//   НомерВхождения 		- Число  - возвращается n вхождение строки.
//   ВключатьТекстДо 		- Булево - признак включения границы в ответ функции.
//   ВключатьТекстПосле 	- Булево - признак включения границы в ответ функции.
//
// Возвращаемое значение:
//   - Строка - если не указан параметр ПолучитьВсеВхождения возвращается первое вхождение подстроки 
//   или n вхождение если указан параметр НомерВхождения отличный от 1
//	 - Массив из Строка - если параметр ПолучитьВсеВхождения указан как Истина возвращается массив подстрок.
//   - Неопределено - если не найдено ни одного вхождения границ в исходную строку.
//
Функция СтрВырезать(Строка, ТекстДо = "", ТекстПосле = "",
					ПолучитьВсеВхождения = Ложь, НомерВхождения = Неопределено,
					ВключатьТекстДо = Ложь, ВключатьТекстПосле = Ложь) Экспорт
					
	Если ПолучитьВсеВхождения Тогда
		НомерВхождения = Неопределено;		
	КонецЕсли;					
	
	ДлинаТекстаДо = СтрДлина(ТекстДо);
	
	ПозицияНачала = СтрНайти(Строка, ТекстДо);
	ПозицияПервогоСимвола = ПозицияНачала + ДлинаТекстаДо;
	ПозицияПоследнегоСимвола = СтрНайти(Строка, ТекстПосле,, ПозицияПервогоСимвола);
	Если ПозицияПоследнегоСимвола = 0 Тогда
		Возврат Неопределено;				
	КонецЕсли;	
	Длина = ПозицияПоследнегоСимвола - ПозицияПервогоСимвола;
	
	ВырезанныйТекст = ВырезатьТекст(Строка, ПозицияПервогоСимвола, Длина, 
									ТекстДо, ТекстПосле, 
									ВключатьТекстДо, ВключатьТекстПосле);
									
	ПолучитьПоНомеру = (НЕ НомерВхождения = Неопределено И НомерВхождения > 1);
	
	Если ПолучитьПоНомеру Тогда
		КоличествоВхожденийТекстДо = СтрЧислоВхождений(Строка, ТекстДо);
		КоличествоВхожденийТекстПосле = СтрЧислоВхождений(Строка, ТекстПосле);
		Если НомерВхождения > КоличествоВхожденийТекстДо Или НомерВхождения > КоличествоВхожденийТекстПосле Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;	

	Если ПолучитьВсеВхождения ИЛИ ПолучитьПоНомеру Тогда
		МассивОтвета = Новый Массив;
		МассивОтвета.Добавить(ВырезанныйТекст);
		ТекущийНомерВхождения = 1;
		Пока ПозицияНачала > 0 Цикл
			ПозицияПервогоСимвола = СтрНайти(Строка, ТекстДо,,ПозицияПервогоСимвола);
			Если ПозицияПервогоСимвола = 0 Тогда 
				Возврат МассивОтвета;
			КонецЕсли;	
			ПозицияПервогоСимвола = ПозицияПервогоСимвола + ДлинаТекстаДо;
			ПозицияПоследнегоСимвола = СтрНайти(Строка, ТекстПосле,, ПозицияПервогоСимвола);
			Если ПозицияПоследнегоСимвола = 0 Тогда
				Возврат МассивОтвета;				
			КонецЕсли;	
			Длина = ПозицияПоследнегоСимвола - ПозицияПервогоСимвола;
			ВырезанныйТекст = ВырезатьТекст(Строка, ПозицияПервогоСимвола, Длина, 
											ТекстДо, ТекстПосле, 
											ВключатьТекстДо, ВключатьТекстПосле);
											
			Если ПолучитьПоНомеру И ТекущийНомерВхождения = НомерВхождения Тогда
				Возврат ВырезанныйТекст;				
			КонецЕсли;	
			МассивОтвета.Добавить(ВырезанныйТекст);
			ПозицияНачала = СтрНайти(Строка, ТекстДо,,ПозицияПервогоСимвола);
		КонецЦикла;
		
		Возврат МассивОтвета;
	КонецЕсли;
	
	Возврат ВырезанныйТекст;
КонецФункции	

Функция ВырезатьТекст(Строка, ПозицияНачала, ДлинаСтроки, 
						ТекстДо, ТекстПосле, 
						ВключатьТекстДо, ВключатьТекстПосле)
	ВырезанныйТекст = Сред(Строка, ПозицияНачала, ДлинаСтроки);
	Если ВключатьТекстДо Тогда
		ВырезанныйТекст = ТекстДо + ВырезанныйТекст;
	КонецЕсли;
	Если ВключатьТекстПосле Тогда
		ВырезанныйТекст = ВырезанныйТекст + ТекстПосле;
	КонецЕсли;
	Возврат ВырезанныйТекст;
КонецФункции

Функция СтрСодержитДанныеИзМассива(Строка, МассивСтрокДляПроверки, СодержитВсеЭлементыМассиваДляПоиска = Ложь) Экспорт

	Если СодержитВсеЭлементыМассиваДляПоиска тогда
		Найдено = Истина;
	Иначе 
		Найдено = Ложь;
	КонецЕсли;
	
	Для Каждого СтрокаДляПроверки из МассивСтрокДляПроверки Цикл
		
		Если СодержитВсеЭлементыМассиваДляПоиска тогда
			Найдено = Найдено И СтрНайти(Строка, СтрокаДляПроверки);
		Иначе
			Найдено = Найдено ИЛИ СтрНайти(Строка, СтрокаДляПроверки);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Найдено;
	
КонецФункции

Функция СтрНайтиМассивСтрокВМассивеСтрок(МассивСтрок, МассивСтрокДляПроверки, СтрокаСодержитВсеЭлементыМассиваДляПоиска = Ложь, МассивСтрокСодержитВсеЭлементыМассиваДляПоиска = Ложь) Экспорт
	
	Если МассивСтрокСодержитВсеЭлементыМассиваДляПоиска Тогда
		Найдено = Истина;
	Иначе
		Найдено = Ложь;
	КонецЕсли;
	
	Для каждого Строка ИЗ МассивСтрок Цикл
		Если МассивСтрокСодержитВсеЭлементыМассиваДляПоиска Тогда
			Найдено = Найдено ИЛИ СтрСодержитДанныеИзМассива(Строка, МассивСтрокДляПроверки, СтрокаСодержитВсеЭлементыМассиваДляПоиска);
		Иначе
			Найдено = Найдено ИЛИ СтрСодержитДанныеИзМассива(Строка, МассивСтрокДляПроверки, СтрокаСодержитВсеЭлементыМассиваДляПоиска);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Найдено;
	
КонецФункции	
